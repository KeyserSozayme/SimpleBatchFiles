@ECHO OFF
SETLOCAL

::==========================::
:: ## Begin Declarations ## ::
::==========================::

SET me=%~n0
SET parentdir=%~dp0
SET varHostname=HostnameNotDefined
SET varEnableSecretPwd=class
SET varEnablePwd=
SET varConsolePwd=cisco
SET varVTYPwd=cisco
SET varSSHUsername=admin
SET varSSHPwd=cisco
SET varIPDomainName=ccna.local
SET varSSHKeyModulus=1024
SET varSSHAllowAccessList=13
SET varSSHAllowHostIP=0.0.0.0

::==============================::
:: ## Main Portion of Script ## ::
::==============================::

ECHO.
ECHO Thank you for using the Cisco Configurator, originally scripted by KeyserSozayme. There is absolutely no input
ECHO sanitization so be careful with what variables you define. Make sure to look over your configuration script
ECHO before copying it into you Router or Switch. I am not responsible for you bjoking your hardware.
ECHO.

CALL :configSetHostname
CALL :configSetEnableSecret
CALL :configSetConsolePwd
CALL :configSetEnablePwd
CALL :configSetVTYPwd
CALL :configSetSSHUsername
CALL :configSetSSHPwd
CALL :configSetIPDomainName
CALL :configSetSSHKeyModulus
CALL :configSetSSHAllowaccessList
CALL :configSetSSHAllowHostIP

SET outputFile=%parentdir%%varHostname% Configuration.txt
CALL :configGenerateOutputFile

ECHO Your output file is located in "%parentdir%" and is titled "%varHostname% Configuration.txt".
PAUSE
EXIT

::====================================::
:: ## Fucnctions are Defined Below ## ::
::====================================::

:configSetHostname
SET /P varHostname="%me% -> Enter a hostname for the device you are configuring: "
GOTO:EOF

:configSetEnableSecret
SET /P varEnableSecretPwd="%me% -> Enter the 'enable secret' Password [Default 'class']: "
GOTO:EOF

:configSetConsolePwd
SET /P varConsolePwd="%me% -> Enter the console password [Default 'cisco']: "
GOTO:EOF

:configSetEnablePwd
SET /P varEnablePwd="%me% -. Enter the enable password [Default '<blank>']"
GOTO:EOF

:configSetVTYPwd
SET /P varVTYPwd="%me% -> Enter the virtual terminal (VTY) Password [Default 'cisco']: "
GOTO:EOF

:configSetSSHUsername
SET /P varSSHUsername="%me% -> Enter the username used to SSH into the device [Default 'admin']: "
GOTO:EOF

:configSetSSHPwd
SET /P varSSHPwd="%me% -> Enter the password used to SSH into the device [Default 'cisco']: "
GOTO:EOF

:configSetIPDomainName
SET /P varIPDomainName="%me% -> Enter the domain name the device is located in [Default 'ccna.local']: "
GOTO:EOF

:configSetSSHKeyModulus
SET /P varSSHKeyModulus="%me% -> Enter the modulus of the SSH key [Default '1024']: "
GOTO:EOF

:configSetSSHAllowAccessList
SET /P varSSHAllowAccessList="%me% -> Enter the ACL # that will permit remote SSH login [Default '13']: "
GOTO:EOF

:configSetSSHAllowHostIP
SET /P varSSHAllowHostIP="%me% -> Enter the IP of the host that will be allowed to SSH into the device [Default '0.0.0.0']: "
GOTO:EOF

:configGenerateOutputFile
(
:: Self Explanitory
ECHO !This file was generated by Cisco Configurator. Scripted by Keith King.

:: Sets the Hostname
ECHO hostname %varHostname%

:: Sets the Enable Secret Password. This Password Must be different from the enable password
ECHO enable secret %varEnableSecretPwd%

:: Sets the Enable Password (If defined)
ECHO enable password %varEnablePwd%

:: Go to Line Console 0
ECHO line con 0

:: Sets the console password
ECHO password %varConsolePwd%

:: Enables the need to enter a password to login to the console
ECHO login

:: Prevents Interruption
ECHO logging synchronous

:: Exits back to global config mode
ECHO exit

:: Encrypts all passwords
ECHO service password-encryption

:: Adds a database entry that will later be used to login to the device with SSH
ECHO username %varSSHUsername% password %varSSHPwd%

:: Defines the domain the device belongs to
ECHO ip domain-name %varIPDomainName%

:: Generate an RSA key for SSH Authentication...
ECHO crypto key generate rsa

:: ...With a defined modulus
ECHO %varSSHKeyModulus%

:: We will use SSH Version 2
ECHO ip SSH version 2

:: Sets an ACL used to specify hosts that are allowed to SSH into our device
ECHO access-list %varSSHAllowAccessList% permit host %varSSHAllowhostIP%

:: Configure the Virtual Terminal lines
ECHO line VTY 0 15

:: Sets the Virtual Terminal passwords
ECHO password %varvtyPwd%

:: Login local
ECHO login local

:: Use SSH instead of Telnet
ECHO transport input SSH

:: Let the previously defined ACL SSH into our device
ECHO access-class %varSShAllowAccessList% in

:: Exit back into global config mode
ECHO exit

:: Go back to 'Main'
ECHO.
) > "%outputFile%"
GOTO:EOF